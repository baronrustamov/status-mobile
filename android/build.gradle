import org.apache.tools.ant.taskdefs.condition.Os

// Top-level build file where you can add configuration options common to all sub-projects/modules.

/**
* Project-wide gradle configuration properties for use by all modules
*/
ext {
    RNNKotlinVersion    = "1.6.20"
    RNGH_kotlinVersion  = "1.6.20"
    buildToolsVersion   = project.buildToolsVersion
    minSdkVersion       = Integer.valueOf(project.minSdkVersion)
    compileSdkVersion   = Integer.valueOf(project.compileSdkVersion)
    targetSdkVersion    = Integer.valueOf(project.targetSdkVersion)
    supportLibVersion   = project.supportLibVersion
    gradlePluginVersion = project.gradlePluginVersion
    kotlinPluginVersion = project.kotlinPluginVersion

    // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
    ndkVersion = "23.1.7779620"
}

buildscript {
    repositories {
        flatDir { dirs "libs", "${rootDir}/app/libs" }
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.ext.kotlinPluginVersion}"
        classpath "com.android.tools.build:gradle:${project.ext.gradlePluginVersion}"
        classpath("com.facebook.react:react-native-gradle-plugin")

        // WARNING: Do not place your application dependencies here!
        //          They belong in the individual module build.gradle files.
    }
}

subprojects {
    afterEvaluate {
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion

                defaultConfig {
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }
            }
        }
    }
}

allprojects {
    beforeEvaluate {
        if (System.env.STATUS_GO_ANDROID_LIBDIR == null || System.env.STATUS_GO_ANDROID_LIBDIR == "") {
            throw new GradleException('STATUS_GO_ANDROID_LIBDIR environment variable is not valid!')
        }
    }

    repositories {
        mavenLocal()
        // For geth, function, and status-go
        flatDir { dirs "libs", System.env.STATUS_GO_ANDROID_LIBDIR }
        // Everything Else
        jcenter() // Required for @react-native-community/blur:3.6.1, can be removed after updating to 3.6.1+
        mavenCentral()
        maven { url 'https://mvnrepository.com/artifact/com.github.gundy/semver4j'}
    }
}


